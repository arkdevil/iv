▄▄                   ▄
▀▀▀ STEALTH GROUP WW █ Mail:   BOX 15, 125080 MOSCOW   ████████ █████████ █▀▀█
▀██ ▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀ █ ▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀ ▀ ▀▀▀▀▐▀▀▀   ┌─┐┬ ┬┌─┤┬ ┬ ╥ ┬┐┌ █▄▄█
 ▐█ █▀▄ █▀▀ ▄▀▀ ▄▀▀ ▄█▄ ▄▀▀ █▀█   ▌ █ ▄▀█ █ ▄▀▀ █▄▄    ├┬┘│ │└─┐├─┤   │└┤ ▄  █
  █ █ █ █▀  █▀  █    █  █▀  █ █   █ █ █ █ █ █   █      ┴└─└─┘└─┘┴ ┴   ┴ ┴ ▀▀▀▀
  █ ▐ ▐ ▐   ▐▄▄ ▐▄▄  ▐  ▐▄▄ ▐▄▀    ▀█ ▀▄█ ▐ ▐▄▄ ▐▄▄▄   ████  WINTER ' 96  ████
  ▐ ▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄ ▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄   ███████████████████████

;██████████████████████████████████████████████████████████████████████████
;██                                                                      ██
;██                     Master boot record         (c) Saxon             ██
;██                                                                      ██
;██████████████████████████████████████████████████████████████████████████
  
  
SEG_A           SEGMENT BYTE PUBLIC
		ASSUME  CS:SEG_A, DS:SEG_A
		ORG     100h
START:                                          
		CLI                     
		XOR     AX,AX                   ;НАСТРОЙКА РЕГИСТРОВ
		MOV     SS,AX
		MOV     SP,7C00H                ;AX=SS=ES=DS=0
		MOV     SI,SP                   ;SP=SI=7C00H
		PUSH    AX
		POP     ES
		PUSH    AX
		POP     DS
		STI                     
		CLD                     
		MOV     DI,600H                 
		MOV     CX,100H
		REPNE   MOVSW                   ;КОПИРУЕМ СЕБЯ ПО АДРЕСУ
						; 0000:0600
		DB      0EAH, 1DH, 06H, 00H, 00H  ;JMP НА СВОЮ ВТОРУЮ КОПИЮ     
				;И ПРОДОЛЖАЕМ ВЫПОЛНЕНИЕ СО СЛЕДУЮЩЕЙ КОМАНДЫ 

;       ТЕПЕРЬ БУДЕМ ПРОСМАТРИВАТЬ ТАБЛИЦУ РАЗДЕЛОВ
;       ОНА СОСТОИТ ИЗ ЧЕТЫРЕХ ПОЛЕЙ ПО 10H БАЙТ КАЖДАЯ
;       ПЕРВОЕ ПОЛЕ НАХОДИТСЯ ПО СМЕЩЕНИЮ 1BEH ОТ НАЧАЛА СЕКТОРА
;       НИЖЕ ПРИВЕДЕН ФОРМАТ ЭТИХ ПОЛЕЙ
;┌───────────────────────────────────────────────────────────────────────────┐
;│OFFSET                SIZE            DESCRIPTION                          │
;│                                                                           │
;│00H           1B              80h=загружаемый раздел,0=незагружаемый       │
;│01H           1B              Начальный номер головки                      │
;│02H           2B              Начальные номера цилиндров( 10 бит) и        │
;│                              сектора (6 бит)                              │
;│04H           1B              Системный индикатор:                         │
;│                                      1-первичный раздел DOS FAT 12        │
;│                                      2-XENIX                              │
;│                                      4-первичный раздел DOS FAT 16        │
;│                                      5-расширенный раздел DOS             │
;│                                      8-ANOTHER                            │
;│05H           1B              Последний номер головки                      │
;│06H           2B              Последние номера цилиндра и сектора          │
;│08H           4B              Начальный сектор (относительно начала диска) │
;│0CH           4B              Число секторов в разделе                     │  
;│                                                                           │          
;└───────────────────────────────────────────────────────────────────────────┘

		MOV     SI,7BEH                 ;ПРОСМОТРИМ ТАБЛИЦУ РАЗДЕЛОВ
		MOV     BL,4                    ;ЧИСЛО ПОЛЕЙ
SCAN_PARTITON:
		CMP     BYTE PTR [SI],80H       ;ЗАГРУЖАЕМЫЙ РАЗДЕЛ?
		JE      LOC_4                   ;ЕСЛИ ДА ТО ПОШЛИ ГРУЗИТЬСЯ 
		CMP     BYTE PTR [SI],0         ;ЕСЛИ ТАМ НЕ 80H И НЕ 0 ТО
						;ТО ВЫВЕДЕМ СООБЩЕНИЕ ОБ ОШИБКЕ
						;(Invalid parti...)
		JNE     LOC_6   

		ADD     SI,10H                  ;НАСТРОИМСЯ НА СЛЕДУЮЩЕЕ ПОЛЕ
		DEC     BL
		JNZ     SCAN_PARTITON                   ;И СМОТРИМ ЕГО
		INT     18H                     ;ЕСЛИ ТАКОВОГО НЕ НАЙДЕНО
						;ТО ПЕРЕКЛЮЧИМСЯ НА ROM basic

LOC_4:
		MOV     DX,[SI]                 ;80H + ГОЛОВКА*100H=>DX
		MOV     CX,[SI+2]               ;CX<=ЦИЛИНДР И СЕКТОР
		MOV     BP,SI                   ;В BP СМЕЩЕНИЕ НАЙДЕННОГО
						;ЭЛЕМЕНТА ТАБЛИЦЫ 


;ТЕПЕРЬ ПРОВЕРИМ ЕСТЬ ЛИ ЕЩЕ РАЗДЕЛЫ ПОМЕЧЕННЫЕ КАК ЗАГРУЖАЕМЫЕ
;ЕСЛИ ТАКОВЫЕ НАЙДУТСЯ ТО ОПЯТЬ ТАКИ 'Invalid partition table'

SCAN_PARTITON_AGAIN:
		ADD     SI,10H
		DEC     BL
		JZ      LOC_9                   ;ЕСЛИ ВСЕ ЭЛЕМЕНТЫ ТАБДИЦЫ
						;ПРОСКАНИРОВАЛИ,ТО ГРУЗИМСЯ
		CMP     BYTE PTR [SI],0
		JE      SCAN_PARTITON_AGAIN                   ;ЕСЛИ НАШЛИ ЕЩЕ ОДИН ЗАГРУЖАЕМЫЙ
						;РАЗДЕЛ ТО ВЫЙДЕМ ИЗ ЦИКЛА
						;И НАПИШЕМ СООБЩЕНИЕ
LOC_6:
		MOV     SI,68BH
LOC_7:
		LODSB           
		CMP     AL,0                    ;КОНЕЦ СООБЩЕНИЯ = 0
		JE      LOC_8                   ;ПРОЧИТАЛИ ? ПРОВИСИТЕ !

		PUSH    SI
		MOV     BX,7
		MOV     AH,0EH
		INT     10H                     ; Video display   ah=functn 0Eh
						; write char al, teletype mode
		POP     SI
		JMP     SHORT LOC_7
LOC_8:
		JMP     SHORT LOC_8		; ВИСИМ-с

;       Ну вот, после проверки Partition table и нахождения загружаемого раз-
;дела можно приступить к самой загрузке 

LOC_9:
		MOV     DI,5
LOC_10:
		MOV     BX,7C00H
		MOV     AX,0201H
		PUSH    DI
		INT     13H                     ; Disk  dl=drive a  ah=func 02h
						; read sectors to memory es:bx
		POP     DI
		JNC     LOC_11                  ;ЕСЛИ СЧИТАЛИ УДАЧНО
						;ТО УЖЕ ПОЧТИ ВСЕ СДЕЛАНО

		XOR     AX,AX                   ;А ЕСЛИ ПРОИЗОШЛА ОШИБКА 
		INT     13H                     ;ТО СБРОСИМ ДИСК
						;AL=return status
		DEC     DI                      ;И ПОПРОБУЕМ ПРОЧИТАТЬ ПОВТОРНО
		JNZ     LOC_10                  ;(ВСЕГО ПЯТЬ ПОПЫТОК см DI)


		MOV     SI,6A3H                 ;ВСЕТАКИ НЕ ЧИТАЕТСЯ ,ТОГДА
		JMP     SHORT LOC_7             ;ПИШЕМ 'Error loadi...'

LOC_11:
		MOV     SI,6C2H 
		MOV     DI,7DFEH                ;ПРОВЕРКА КОНТРОЛЬНЫХ БАЙТ (Uк)
		CMP     WORD PTR [DI],0AA55H
		JNE     LOC_7                   ;ЕСЛИ МЫ СЧИТАЛИ НЕ ЗАГРУЗОЧ-
						;НЫЙ BOOТ SECTOR 
						;ТО 'Missing opera..'
		MOV     SI,BP                   ;ВЕРНЕМ В SI СМЕЩЕНИЕ Partition

		DB      0EAH, 00H, 7CH, 00H, 00H ;И ОТДАЕМ УПРАВЛЕНИЕ НА BOOT
						 ;(0000:7C00)   

MES_008B        DB      'Invalid partition table', 0
MES_00A3        DB      'Error loading operating system', 0
MES_00C2        DB      'Missing operating system'
		DB      228 DUP (0)

;1BEH           Partition table
		;1-Й РАЗДЕЛ
		DB       80H            ;ИНДИКАТОР ЗАГРУЗКИ   (загружаемый)                
		DB       01H            ;НАЧАЛЬНЫЙ НОМЕР ГОЛОВКИ
		DW       0001H          ;НАЧАЛЬНЫЕ НОМЕРА ЦИЛИНДРА И СЕКТОРА
		DB       01H           ;СИСТЕМНЫЙ ИНДИКАТОР (первый DOS FAT12)
		DB       0FH            ;ПОСЛЕДНИЙ НОМЕР ГОЛОВКИ
		DW       1E3FH          ;ПОСЛЕДНИЕ НОМЕРА ЦИЛИНДРА И ГОЛОВКИ
		DD       0000003FH      ;НАЧАЛЬНЫЙ СЕКТОР (ОТНОСИТЕЛЬНО
					;НАЧАЛА ДИСКА)
		DD      000079D1H       ;ЧИСЛО СЕКТОРОВ В РАЗДЕЛЕ
		
		;2-Й РАЗДЕЛ
		DB       00H            ;НЕЗАГРУЖАЕМЫЙ
		DB       00H                    
		DW       1F01H
		DB       05H            ;РАСШИРЕННЫЙ РАЗДЕЛ DOS
		DB       0FH
		DW       0FEFFH
		DD       00007A10H
		DD       000F4200H
		
		;3-Й И 4-Й РАЗДЕЛЫ
		DB      32 DUP (0)       ;НУЛИ
		
		DB       55H,0AAH        ;Uк-ИНДИКАТОР BOOT СЕКТОРА 
  
SEG_A           ENDS
		END     START
  
